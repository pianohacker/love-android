apply plugin: 'com.android.application'

android {
    compileSdkVersion 31
    buildToolsVersion '31.0.0'
    ndkVersion '21.3.6528147'
    defaultConfig {
		def Properties versionProps = new Properties()
		versionProps.load(new FileInputStream(file('../version.properties')))

        applicationId 're.weav.android.bucket'
        versionCode versionProps['VERSION_CODE'] as int
        versionName versionProps['VERSION_NAME']
        minSdkVersion 16
        targetSdkVersion 31
    }
    signingConfigs {
        release {
            def Properties keyProps = new Properties()
            keyProps.load(new FileInputStream(file('../keystore.properties')))

            storeFile file(keyProps["RELEASE_STORE_FILE"])
            keyAlias keyProps["RELEASE_KEY_ALIAS"]
            storePassword keyProps["RELEASE_STORE_PASSWORD"]
            keyPassword keyProps["RELEASE_KEY_PASSWORD"]
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    flavorDimensions 'mode', 'recording'
    productFlavors {
        normal {
            dimension 'mode'
        }
        embed {
            dimension 'mode'
        }
        record {
            dimension 'recording'
        }
        noRecord {
            dimension 'recording'
        }
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    api 'androidx.multidex:multidex:2.0.1'
    api fileTree(dir: 'libs', include: ['*.jar'])
    api 'androidx.appcompat:appcompat:1.4.0'
    api project(':love')
}
